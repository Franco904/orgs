plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.example.orgs"
        minSdk 22
        targetSdk 33
        versionCode 1
        versionName "1.0"

        // Runner for instrumented tests (JUnit 4)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
                arg("room.incremental", "true")
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    packagingOptions {
        pickFirst 'META-INF/LICENSE.md'
        pickFirst 'META-INF/LICENSE-notice.md'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDirs = [
                    'src/androidTest/java',
                    sharedTestDir,
            ]
        }
    }
}

dependencies {
    // Lib versions
    def room_version = "2.4.3"
    def kotlin_coroutines_version = "1.6.4"
    def lifecycle_version = "2.5.1"
    def junit_jupiter_version = "5.9.2"
    def faker_version = "1.13.0"
    def arch_version = "1.0.0-alpha5"

    // Annotation processor
    kapt "androidx.room:room-compiler:$room_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Core
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // UI
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.activity:activity-ktx:1.6.1"
    implementation "com.google.android.material:material:1.8.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "com.android.databinding:viewbinding:7.4.2"
    implementation 'io.coil-kt:coil:2.2.2'

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "android.arch.persistence.room:testing:$arch_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_version"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // DataStore (Preferences)
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Dagger-Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"

    // JUnit 4 (Only for Android Tests)
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation 'org.junit.platform:junit-platform-runner:1.8.1'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // JUnit 5 (Only for Unit Tests)
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"

    // Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:rules:1.5.0"

    // Test Helpers
    implementation "io.github.serpro69:kotlin-faker:$faker_version"
    testImplementation "org.amshove.kluent:kluent-android:1.72"
    testImplementation "io.mockk:mockk:1.13.4"
}